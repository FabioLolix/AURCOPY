# Maintainer: Lukas Spies <lukas (at) photoqt _dot_ org>
# Contributor: archtux <antonio dot arias99999 at gmail dot com>

pkgname=photoqt
pkgver=4.7
pkgrel=1.1
pkgdesc="Fast and highly configurable image viewer with a simple and nice interface."
arch=(x86_64)
url="https://photoqt.org/"
license=(GPL-2.0-or-later)
depends=(exiv2 imagemagick qt6-imageformats qt6-multimedia qt6-svg qt6-declarative qt6-location qt6-positioning
         libraw hicolor-icon-theme libarchive kimageformats pugixml mpv 'resvg>=0.43.0' python-pychromecast
         qt6-webengine zxing-cpp lcms2)
optdepends=('libqpsd: PSB/PSD support'
            'xcftools: XCF support')
makedepends=(cmake qt6-tools extra-cmake-modules)
source=(https://photoqt.org/downloads/source/$pkgname-$pkgver.tar.gz)
sha256sums=('b9909e249b10a080d9ea77fe25b1616d71386472fca42634a49386e70eafe2fb')

# NOTE
# To use GraphicsMagick instead of ImageMagick replace it in the depends array above and change
# '-DIMAGEMAGICK=ON -DGRAPHICSMAGICK=OFF' to '-DIMAGEMAGICK=OFF -DGRAPHICSMAGICK=ON' in the cmake call below.
#
# If you want to build PhotoQt without python-pychromecast remove it from the depends array
# and change '-DCHROMECAST=ON' to '-DCHROMECAST=OFF' in the cmake call below.
#
# These dependencies are currently disabled in the cmake call below:
# 'freeimage' 'devil-ilut' 'poppler-qt6' 'libvips'

build() {
  local _flags=(
    -DFREEIMAGE=OFF
    -DDEVIL=OFF
    -DPOPPLER=OFF
    -DQTPDF=ON
    -DIMAGEMAGICK=ON
    -DGRAPHICSMAGICK=OFF
    -DLIBVIPS=OFF
    -DVIDEO_MPV=ON
    -DCHROMECAST=ON
    -DRESVG=ON
    -DZXING=ON
    -DLCMS2=ON
  )

  cmake -B build -S "photoqt-${pkgver}" -Wno-dev \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    "${_flags[@]}"

  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
