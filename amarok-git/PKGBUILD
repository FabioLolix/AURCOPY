# Maintainer: Mihkel Tõnnov <mihhkel-at-gmail-dot-com>
# Contributor: Rhinoceros <https://aur.archlinux.org/account/rhinoceros>
# Contributor: Evert Vorster <evorster-at-gmail-dot-com>
# Contributor: Luca Weiss <luca (at) z3ntu (dot) xyz>
# Contributor: Jakub Schmidtke <sjakub-at-gmail-dot-com>
# Contributor: Maël Kerbiriou <mael.kerbiriou-at-free-dot-fr>

pkgname=amarok-git
pkgver=3.1.0.r1.g03ef4db
pkgrel=1
pkgdesc="The powerful music player for KDE"
arch=(i686 x86_64)
url="https://apps.kde.org/amarok/"
license=(GPL2 LGPL2.1 FDL)
depends=(threadweaver5 qt5-webengine phonon-qt5 qt5-declarative #qt5-script
         kcmutils5 knewstuff5 ktexteditor5 kdnssd5 kirigami2
         mariadb libmariadbclient fftw liblastfm-qt5 ffmpeg  taglib libofa)
makedepends=(git extra-cmake-modules kdoctools5 gdk-pixbuf2 knotifyconfig5
             libmtp libgpod loudmouth libmygpo-qt5 qt5-tools gtest)
checkdepends=(xorg-server-xvfb)
optdepends=("libmtp: support for portable media devices"
            "ifuse: support for Apple iPod Touch and iPhone"
            "libgpod: support for Apple iPod audio devices"
            "loudmouth: backend needed by mp3tunes for syncing"
            "libmygpo-qt5: gpodder.net Internet Service"
            "taglib-extras: taglib plugins for Audible and RealMedia files")
conflicts=(amarok)
provides=(amarok)
source=("git+https://invent.kde.org/multimedia/amarok.git")
sha512sums=("SKIP")

pkgver() {
  cd "$srcdir/amarok"
  git describe --long --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cmake -B build -S "amarok" -Wno-dev \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr

  cmake --build build
}

check() {
  xvfb-run -s '-screen 0 1920x1080x24 -nolisten local' ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
}
